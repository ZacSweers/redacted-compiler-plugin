plugins {
    id("org.jetbrains.kotlin.jvm") version "1.6.0-RC"
    id("java-gradle-plugin")
    id("org.jetbrains.dokka") version "1.5.30"
    id("com.vanniktech.maven.publish") version "0.18.0"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(8)
}

//region Version.kt template for setting the project version in the build
sourceSets {
    main.java.srcDirs += "$buildDir/generated/sources/version-templates/kotlin/main"
}

TaskProvider copyVersionTemplatesProvider = tasks.register("copyVersionTemplates", Copy) {
    it.inputs.property("version", VERSION_NAME)
    from project.layout.projectDirectory.dir("version-templates")
    into project.layout.buildDirectory.dir("generated/sources/version-templates/kotlin/main")
    expand('projectVersion': "${VERSION_NAME}")
    filteringCharset = 'UTF-8'
}
//endregion

compileKotlin {
    dependsOn copyVersionTemplatesProvider
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

gradlePlugin {
    plugins {
        redactedPlugin {
            id = 'dev.zacsweers.redacted'
            implementationClass = 'dev.zacsweers.redacted.gradle.RedactedGradleSubplugin'
        }
    }
}

tasks.named("dokkaHtml") {
    outputDirectory.set(rootProject.file("../docs/0.x"))
    dokkaSourceSets.configureEach {
        skipDeprecated.set(true)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:1.6.0-RC"
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0-RC"
    implementation "com.google.auto.service:auto-service-annotations:1.0"
}