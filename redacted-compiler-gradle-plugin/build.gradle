// Yank properties from shared file
Properties props = new Properties()
file("../gradle.properties").withInputStream { props.load(it) }
props.each { key, val ->
    ext.set(key, val)
}
buildscript {
    Properties buildScriptProps = new Properties()
    file("../gradle.properties").withInputStream { buildScriptProps.load(it) }
    buildScriptProps.each { key, val ->
        ext.set(key, val)
    }
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:${maven_publish_plugin_version}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:${binary_validator_version}"
    }
}

apply plugin: "java-gradle-plugin"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: 'org.jetbrains.dokka'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: "binary-compatibility-validator"

repositories {
    google()
    jcenter()
    maven {
        url = "https://dl.bintray.com/kotlin/kotlin-eap"
        name = "Kotlin-eap"
        content {
            // this repository *only* contains Kotlin artifacts (don't try others here)
            includeGroupByRegex("org\\.jetbrains.*")
        }
    }
}

//region Version.kt template for setting the project version in the build
sourceSets {
    main.java.srcDirs += "$buildDir/generated/sources/version-templates/kotlin/main"
}

TaskProvider copyVersionTemplatesProvider = tasks.register("copyVersionTemplates", Copy) {
    from rootProject.layout.projectDirectory.dir("version-templates")
    into project.layout.buildDirectory.dir("generated/sources/version-templates/kotlin/main")
    expand('projectVersion': "${project.version}")
    filteringCharset = 'UTF-8'
}
//endregion

compileKotlin {
    dependsOn copyVersionTemplatesProvider
    kotlinOptions {
        jvmTarget = System.getenv().getOrDefault("ci_java_version", "1.8")
        freeCompilerArgs += ['-progressive']
    }
}

gradlePlugin {
    plugins {
        redactedPlugin {
            id = 'dev.zacsweers.redacted.redacted-gradle-plugin'
            implementationClass = 'dev.zacsweers.redacted.gradle.RedactedGradleSubplugin'
        }
    }
}

tasks.named("dokkaHtml") {
    outputDirectory.set(rootProject.file("../docs/0.x"))
    dokkaSourceSets.configureEach {
        skipDeprecated.set(true)

        // Suppress a package
//        perPackageOption {
//            matchingRegex.set(/.*\.internal.*/)
//            suppress.set(true)
//        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    compileOnly "com.google.auto.service:auto-service-annotations:$autoservice_version"
    kapt "com.google.auto.service:auto-service:$autoservice_version"
}